window "show"
page = "APPSTORE// Loading"
permission "request" "file admin"
permission "request" "terminal"
permission "request" "save admin"

window "dimensions" 1000 600
save "appstore@system" "set_directory"
if "installed.json".saveExists().not (
  save "installed.json" "set" {}
)

def "tab" "name"
  if name == page "c #333" else "c #222"
  square sidebar_width - 15 40 10 : hover_c#333
  if clicked "page = name"
  change_y -60
endef

icons = {}

mainloop:

ww = window_width
wh = window_height
sidebar_width = ww / 5
if sidebar_width < 300 "sidebar_width = 300"
loc 999 2 0 -20
square ww 40 10 1 : c#111
loc 2 2 sidebar_width + 20 -20
text page_name 9 : c#fff

c #333
frame ww / -2 + sidebar_width + 10 wh / 2 - 50 ww / 2 wh / -2 len

if page == "APPSTORE// Loading" (
  page_name = "Loading..."
  direction timer * 360
  icon "cutcircle 0 0 10 0 90" 5 : c#fff
  direction 90
  network "get" "https://raw.githubusercontent.com/Mistium/apps/main/apps.json"
  if data != "loading" (
    apps = data.replace(newline,"").destr
    page = "APPSTORE// Store"
  )
)

if page == "APPSTORE// store" (
  page_name = "Store"
  app_h = frame_width / 2
  count = (scroll_y / 70).round - 1
  len = apps.len
  loop (frame_height / 70).ceiling.clamp(0,len) + 1 (
    count ++
    y = count - 0.5 * -70 + scroll_y
    loc 999 2 0 y
    cur = apps[count]
    square frame_width - 20 50 10 : c#222 hover_c#333
    if clicked (
      page = cur
      app_tab = "Description"
      installed = 0
    )
    loc 2 2 30 y
    if icons.contains(cur).not (
       network "get" "https://raw.githubusercontent.com/Mistium/apps/main/all/" ++ cur.to("url") ++ "/icon.icn"
       if data.str != "loading" (
         icons[cur] = data.replace(newline,"")
      ) 
    )
    icon icons.[cur] 1.5
    loc 2 2 60 y
    text cur.toTitle() 10 : c#fff
  )
  len = apps.len - 0.5 * 70
)

if page == "APPSTORE// profile" (
  page_name = "Profile"
)

if page == "APPSTORE// library" (
  page_name = "Library"
  library_apps = ( user_folder ++ "/Applications" ).getFolder()
  app_h = frame_width / 2
  count = ( scroll_y / 70 ).round - 1
  len = library_apps.len
  loop (frame_height / 70).ceiling.clamp(0,len) + 1 (
    count ++
    y = count - 0.5 * -70 + scroll_y
    loc 999 2 0 y
    file "open" library_apps[count] "onlyaccess"
    square frame_width - 20 50 10 : c#222 hover_c#333
    loc -2 2 -35 y
    square 30 30 10 : c#333
    icon "play" 0.8 : c#fff
    if onclick (
      file "start"
    )
    loc 2 2 35 y
    file "render" 1.5
    loc 2 2 60 y
    file "get" 2
    text data.toTitle() 10 : c#fff
  )
  len = library_apps.len - 0.5 * 70
)

if page == "APPSTORE// search" (
  page_name = "Search"
)

app_page = page.contains("APPSTORE// ").not

if app_page (
  network "get" "https://raw.githubusercontent.com/Mistium/apps/main/all/" ++ page.to("url") ++ "/info.json"
  if data == "Loading" (
    direction timer * 360
    icon "cutcircle 0 0 10 0 90" 5 : c#fff
    direction 90
  ) else (
    current_app = data
    cur = current_app."title".toLower()
    if installed == 0 and cur != "" (
      save "installed.json" "get"
      app_path = user_folder ++ "/applications/" ++ cur ++ ".osl"
      file "exists" app_path
      installed = save_data.contains(cur) and exists
      install_version = save_data[cur].int
    )
    desc = current_app."description".str
    w = frame_width
    loc 999 2 0 -50
    square w 130 10 1 : c#222
    loc 2 2 60 -60
    icon icons[cur].str 3
    loc 2 2 140 -50
    text cur.toTitle() + "(v" ++ current_app.version ++ ")" 10 : c#fff
    loc 2 2 140 -80
    text "By" + current_app."author" 9
    loc 2 2 20 -160
    text desc.wrapText((window_width - 350) / 9) 9
    if installed "loc -2 2 -90 -35" else "loc -2 2 -90 -60"
    square 150 30 15 : c#global_accent
    square 150 30 10 : c#333 hover_c#444
    if onclick (
      if installed and current_app.version == install_version (
        file "open" app_path "onlyaccess"
        file "start"
        file "close"
      ) else (
        terminal "app install" + cur.str
        save "installed.json" "get"
        install_version = current_app.version
        save_data[cur] = install_version
        save "installed.json" "set" save_data
        say cur + "Has been installed to /Applications"
        installed = true
      )
    )
    c #fff
    change_x -60
    if installed (
      if current_app.version == install_version (
        icon "play" 0.7
        text "Open" 9 : chx#20
      ) else (
        icon "reload" 0.7
        text "Update" 9 : chx#20
      )
      loc -2 2 -90 -85
      square 150 30 15 : c#global_accent
      square 150 30 10 : c#333 hover_c#444
      if onclick (
        terminal "app uninstall" + cur.str
        save "installed.json" "get"
        save "installed.json" "set" save_data.delete(cur,version)
        say cur + "Has been uninstalled"
        installed = false
      )
      change_x -60
      icon "Bin" 0.7 : c#fff
      text "Uninstall" 9 : chx#20
    ) else (
      icon "download" 0.7
      text "Install" 9 : chx#20
    )
    len = 0
  )
)
frame "clear"

loc 2 999 sidebar_width / 2 0
square sidebar_width wh 20 1 : c#111

loc 2 2 sidebar_width / 2 + 3 -30
tab "APPSTORE// Profile"
change_y -15
tab "APPSTORE// Store"
tab "APPSTORE// Search"
tab "APPSTORE// Library"

loc 2 2 30 -30
image user_icon.str 30
change 30
text username 10 : c#fff

loc 2 2 30 -105
icon "w 3 line -5 2 -5 5 line 5 2 5 5 cutcircle 0 5 5 0 90 square 0 -4 10 5" 1
change_x 20
text "Store" 9
loc 2 2 30 -165
icon "search" 1
change_x 20
text "Search" 9
loc 2 2 30 -225
icon "w 3 line 0 6 10 10 cont 10 -6 cont 0 -10 cont 0 6 cont -10 10 cont -10 -6 cont 0 -10" 1
change_x 20
text "Library" 9

loc 2 -2 sidebar_width / 2 + 5 30
c #222
square sidebar_width - 15 30 10 : hover_c#333
loc 2 -2 30 30
c #fff
icon "upload" 0.7
change_x 20
text "Upload Your Apps" 9
if onclick (
  http "newtab" "https://github.com/Mistium/apps/tree/main"
)

if mouse_down.not "can = true"
import "win-buttons"
