permission "request" "save admin"
save "contacts" "set_directory"
import "window_tools"
seticon "c #000 w 10 square 0 0 5 5 w 7 c #222 square -2 0 3 5 w 3 dot 7 4.5 dot 7 0 dot 7 -4.5 c #fff icn accounts 0.5 -2 0"

window "show"
save "contacts.json" "exists"
if save_data (
  save "contacts.json" "get"
  contacts = save_data
) else (
  save "contacts.json" "set" []
  contacts = []
)
contacts_add = ""

current_view = ""
mainloop:

if new_network_data (
  if network_data_command == "find_account" (
    if network_data != "Account Not Found" (
      contacts = contacts + network_data
      save "contacts.json" "set" contacts
    )
  )
  new_network_data = false
)
loc 999 2 0 -20
square window_width 30 15 : c#222

sidewidth = window_width / 3
loc 3 999 0 0
square sidewidth window_height 15 : c#222
loc 3 2 0 -20
square sidewidth 30 15 : c#333
loc 3 2 -36 -20
text "Contacts" 9 : c#fff
loc 2 2 20 -20
icon "add" 0.8 : hover_size#1.1
if clicked and can (
  can = false
  contacts_add = "Enter an origin username".ask.str
  if contacts_add == null (
    contacta_add = ""
  )
)
c #555
frame window."left" window."top" - 47.5 window."left" + sidewidth + 5 window."bottom" contacts.len * 45
count = 0
loop contacts.len (
  count ++
  cur = contacts.[count]
  y = count - 0.5 * -50 + scroll_y
  loc 2 2 frame_width / 2 y
  c cur."accent"
  square frame_width - 20 25 16
  if clicked (
    current_view = cur
  )
  square frame_width - 20 25 10 : c#333 hover_c#444
  loc 2 2 22 y
  image cur."pfp" 30 30
  text cur."username" 10 : chx#30 c#fff
  loc 2 2 frame_width - 20 y
  icon "close" 0.7
  if clicked and can (
    can = false
    contacts = contacts.delete(count)
    save "contacts.json" "set" contacts
    count --
  )
)
frame "clear"
if mouse_down.not (
  can = true
)
if current_view != "" (
  frame window."left" + sidewidth window."top" - 47.5 window."right" window."bottom"
  loc 999 2 0 -100
  name = current_view."username"
  image current_view."pfp" 180 180
  loc 999 2 name.len * -5 -210
  text name 10 : c#fff
  loc 999 2 -120
  frame "clear"
)
if contacts_add != "" (
  network "find_account" contacts_add
  contacts_add = ""
)
import "win-buttons"
