permission "request" "account"
window_y = screensize_y * -1.2
window "dimensions" screensize_x screensize_y

network "my_files_usage"

def "openapp" "name"
  window "add" name
  window "stop"
endef

def "find_sizes"
  use1 = usage[1].int / 1000
  unit = "kb"
  if use1 > 1000 (
    unit = "mb"
    use1 = use1.int / 1000
    if use1 > 1000 (
      unit = "gb"
      use1 = use1.int / 1000
    )
  )
  use2 = usage[2].int / 1000000
  unit2 = "mb"
  if use2 > 1000 (
    unit2 = "gb"
    use2 = use2.int / 1000
  )
  use1 = (use1 * 10).round / 10
  use2 = (use2 * 10).round / 10
endef

target_y = 0
usage = []
use1 = "none"

v = origin.version
version = v.trim(1,v.len - 2) ++ "." ++ v[v.len - 1] ++ "." ++ v[v.len]

mainloop:

if new_network_data (
  if network_data_command == "my_files_usage" (
    usage = network_data
    log usage
    new_network_data = false
  )
)

window_y += target_y - window_y / 5
window "y" window_y.round

square 700 700 15 : c#global_accent
square 700 700 10 1 : c#000
if mouse_down and clicked.not "window stop"

goto 0 0
square 0 700 10 : c#111
goto 0 325
square 700 50 10 : c#151515
goto 0 -325
square 700 50 10
goto -325 325
image user_icon.str 40

goto 325 -325
icon "power" 1 : hover_size#1.05 c#fff
if clicked (
  terminal "shutdown"
)

icon "settings" 1 : chx#-50 hover_size#1.05
if clicked (
  openapp "settings"
)

icon "w 1.5 line 0 -4 5 -4 line -2 0 -6 4 line -2 0 -6 -4" 2 : chx#-50 hover_size#1.05
if clicked (
  openapp "terminal"
)

if hour < 12 (
  msg = "Good Morning" + username
) else (
  msg = "Good Afternoon" + username
)
goto -290 325
text msg 13 : c#fff

goto 325 325
icon "close" 1
if clicked (
  window "stop"
)

goto -180 230
square 320 100 10 : c#222

if usage.len == 0 (
  change_x -100
  text "Loading Storage Info" 10 : c#fff
) else (
  if use1 == "none" "find_sizes"
  goto -245 260
  text "Storage" 10 : c#fff
  goto -245 230
  text `Used ${use1}${unit} / ${use2}${unit2}` 9
  goto -140 200
  bar 200 20 10 usage[3] : c#111
  usepercent = (usage[3] * 100).round ++ "%"
  goto -295 - (usepercent.len * 8) 230
  text usepercent 16 : c#fff
)

goto -180 110
square 320 100 10 : c#738bd7
if mouse_touching (
  cursor "pointer"
  if clicked (
    http "newtab" "https://discord.gg/aujmXHtgXA"
  )
)
image "https://discordapp.com/api/guilds/1147362734300725298/widget.png?style=banner2" 320

network "get" "https://api.github.com/repos/Mistium/Origin-OS"
repo_data = data.str
goto -180 -10
square 320 100 10 : c#333
if mouse_touching (
  cursor "pointer"
  if clicked (
    http "newtab" "https://github.com/Mistium/Origin-OS"
  )
)
goto -290 -10
image "https://raw.githubusercontent.com/Mistium/Origin-OS/main/Resources/github_icon.png" 60
goto -230 2
text "Github" 13 : c#fff
goto -222 -27
icon "w 3 line 0 9.5 5 3 cont 10 3 cont 5 -2 cont 7 -10 cont 0 -5 cont -7 -10 cont -5 -2 cont -10 3 cont -5 3 cont 0 9.5" 0.7
text data."stargazers_count" 10 : chx#15
icon "eye" 0.7 : chx#30
text data."subscribers_count" 10 : chx#15

goto -180 -130
square 320 100 10 : c#111
change_x -110
icon "origin-logo" 2.5 : c#fff
change 50 10
c #fff
text "originOS" 13
change -50 -20
text "v" ++ version 10
