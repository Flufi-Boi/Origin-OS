// Developer: @Mistium
// Description: This app shows quick info about your device
permission "request" "account"
permission "request" "terminal"
settings_open = false
can = false
window "front"
last_synced = 0
window_x = screensize_x / 2 + 128
window_target_x = -102
used = "used"
error = ""
window "dimensions" 190 380
window "responsive" true
can = false
syncing = true
network "my_files_usage"
last_check = timer
quick_settings_x = 300
target = 300
mainloop:

direction 90

if timer - last_check_2 > 0.5 (
  terminal "get filechanges"
  changes = data.len
  last_check_2 = timer
  notifications = []
  if timer - last_check > 300 (
    network "my_files_usage"
    last_check = timer
  )
  bg_colour = user."dock_colour"
  icn_colour = user."icon_colour"
  if new_network_data (
    if network_data_command == "my_files_usage" (
      used = network_data
      use1 = used.[1] / 1000
      unit = "kb"
      if use1 > 1000 (
        unit = "mb"
        use1 /= 1000
        if use1 > 1000 (
          unit = "gb"
          use1 /= 1000
        )
      )
      use2 = used.[2] / 1000
      unit2 = "kb"
      if use2 > 1000 (
        unit2 = "mb"
        use2 /= 1000
        if use2 > 1000 (
          unit2 = "gb"
          use2 /= 1000
        )
      )
      use1.round
      use2.round
    )
    network_data_command = null
    new_network_data = false
  )
)

if changes == 0 (
  last_sync_text = "Files are synced"
) else (
  if changes == 1 (
    last_sync_text = "1 action"
  ) else (
    last_sync_text = `${changes} actions`
  )
)

window_x += window_target_x - window_x / 10
window "x" screensize_x / 2 + window_x
window "y" screensize_y / 2 - 200

loc 999 2 0 -83
square 170 30 20 0 1
if bg_redrawn or mouse_touching or ( last_sync_text != sync_text_temp ) (
  sync_text_temp = last_sync_text
  loc 999 2 0 -83
  square 170 30 20 : c#bg_colour
  change_x -70
  c icn_colour
  direction 90
  icon "w 3 cutcircle 0 0 10 -4 65 line 0 15 4.5 9 cont -1 5 cutcircle 0 0 10 14 65 line 0 -15 -4.5 -9 cont 1 -5" 0.7 : hover_size#1.05
  change 30 -10
  text last_sync_text 8
  loc 999 2 -40 -77
  if used != "used" (
    text `${use1}${unit} / ${use2}${unit2}` 8
  ) else (
    text "Unknown Usage Data" 8
  )
)

goto 0 0
square 170 window_height 20 0 1
if bg_redrawn or mouse_touching (
  loc 999 2 0 -25
  square 170 30 20 0 1
  if bg_redrawn or mouse_touching (
    if onclick (
      settings_cha = true
    )
    if mouse_down.not (
      if settings_cha (
        if mouse_touching "settings_open = settings_open.not"
        settings_cha = false
        if settings_open.not (
          window "refresh_bg"
        )
      )
    )
    loc 999 2 0 -25
    square 170 30 20 : c#bg_colour

    loc -2 2 -25 -25
    c icn_colour
    if settings_open (
      icon "w 4 line 10 5 0 -5 cont -10 5" 0.7
      target = 0
    ) else (
      icon "w 4 line 10 -5 0 5 cont -10 -5" 0.7
      target = 300
    )
    image user_icon.str 25 : chx#-46
    icon "sun" 0.8 : chx#-46
    c icn_colour
    icon "w 3 cutcircle 0 -6 14 0 50 cutcircle 0 -6 7 0 50 w 4 dot 0 -6" 0.8 : chx#-46
    if network."connected".not (
      icon "w 3 line 10 10 -10 -10" 0.8
    )
  )
  if settings_open or quick_settings_x.round < 300 (
    if target == 300 or ( target == 0 and quick_settings_x > 3 ) (
      window "refresh_bg"
    )
    quick_settings_x += ( target - quick_settings_x ) / 5
    loc 999 2 quick_settings_x -203
    square 170 155 20 : c#bg_colour

    loc 999 2 quick_settings_x -170
    terminal "system display get brightness"
    slider_brightness = data - 10 / 90
    slider 130 0 "brightness" 32 : c#bg_colour chy#20
    terminal "system display set brightness" + ( slider_brightness * 90 + 10 )

    c bg_colour
    icon "w 25 dot" 1 : chx#-65
    icon "sun" 0.6 : c#icn_colour
    change 65 -45

    terminal "system sound get volume"
    slider_volume = data / 100
    slider 130 0 "volume" 32 : c#bg_colour
    terminal "system sound set volume" + ( slider_volume * 100 )

    c bg_colour
    icon "w 25 dot" 1 : chx#-65
    icon "volume-max" 0.7 : c#icn_colour

    loc 2 2 quick_settings_x + 30 -237.5
    square 0 0 25 0 1
    if onclick (
      window "add" "StandbyMode"
    )
    icon "Moon" 0.7 : c#icn_colour

    change_x 34.4
    terminal "flags performance_mode get"
    if data "c global_accent" else "c bg_colour"
    square 0 0 25 : hover_size#1.05
    if onclick (
      terminal "flags performance_mode set" + data.not
    )
    icon "w 2.5 cutcircle 0 0 10 0 135 line 0 0 4 -4 line 0 8 0 7 line 6 6 5 5 line -6 6 -5 5 line -8 0 -7 0 line 8 0 7 0 line -7 -7 -5 -5" 0.7 : c#icn_colour

    change_x 34.4
    terminal "flags nightmode get"
    if data "c global_accent" else "c bg_colour"
    square 0 0 25 : hover_size#1.05
    if onclick (
      terminal "flags nightmode set" + data.not
    )
    icon "sun" 0.7 : c#icn_colour

    date = day + day_number + month.trim(1,3)
    loc 999 2 quick_settings_x + ( date.len + 0.5 * -4 ) -277
    text date 8
  )
  if notifications != [] (
    y = quick_settings_x - 400 / 1.63 - 75
    loc 999 2 0 y
    square 170 25 20 : c#bg_colour
    cur = notifications.[1]
    change_x -70
    icon cur.[1].str 1 : c#icn_colour
    change 25 6
    start_x = x_position
    text cur.[2].str 8
    set_x start_x
    change_y -17
    text cur.[3].str 7
    if notifications.len > 1 (
      loc 999 2 0 y - 40
      square 65 15 10 : c#bg_colour
      change_x -32
      text "View All" 8 : c#icn_colour
    )
  )
)
if mouse_down.not "can = true"
