def "appopt" "optname,opticn,icnsize"
  loc 999 2 0 y
  square frame_width - 25 30 10 1 : c#222
  loc 2 2 30 y
  icon opticn icnsize.destr : c#fff
  change_x 30
  text optname 8
endef

def "DrawCategory" "setting,icn_code"
  temp = x_position
  if category == setting "c #444" else "c #222"
  square 190 30 10 1 : chx#80
  c #333
  if mouse_touching "square 190 30 15 1"
  if clicked (
    category = setting
    subcategory = "none"
  )
  icon icn_code 0.8 : c#fff chx#-80
  change 20
  text setting 10
  goto temp y_position - 50
endef

def "option" "name,icn_code"
  loc 999 2 0 y
  square frame_width - 25 30 10 1 : c#222
  if mouse_touching and clicked.not "square frame_width - 25 30 12 1 : c#252525"
  if clicked "gotosubcategory = name"
  change_x frame_width - 25 / -2 + 20
  if icn_code == "" "icn_code = name"
  icon icn_code 1 : c#fff
  text name 10 : c#fff chx#30
  y -= 50
  page_len += 50
endef

def "system"
  if network_drive.istype("array").not (
    mem = "navigator.deviceMemory".eval
    cores = "navigator.hardwareConcurrency".eval

    if network_drive == "none" and ( tier == null ) (
      network "my_files_usage"
      tier = "Loading..."
      unit = null
    )
    if new_network_data (
      if network_data_command == "my_files_usage" (
        network_drive = network_data.destr
        log "sys" network_drive.[2]
        if network_drive.[2] == 100000000 (
          tier = "Free Tier"
        )
        if network_drive.[2] == 500000000 (
          tier = "Supporter"
        )
        if network_drive.[2] == 1000000000 (
          tier = "originDrive"
        )
        if network_drive.[2] == 5000000000 (
          tier = "originPro"
        )
        if username == "Mist" (
          tier = "Admin"
        )
        if network_drive.[2] > 999999 (
          used_big = network_drive.[2].divide(1000000).round
          unit = "mb"
        )
        if network_drive.[2] > 999999999 (
          used_big = network_drive.[2].divide(1000000000).round
          unit = "gb"
        )
      )
      new_network_data = false
    )
  )
  if subcategory == "none" (
    option "Info" "w 2 cutcircle 0 0 10 0 180 w 3 line 0 0 0 -5 dot 0 5"
    option "Display" "w 3 square 0 3 10 7 w 3 line 0 -7 0 -10 line -6 -10 6 -10"
    option "Media" "w 2 cutcircle -3 -1.5 6.5 -9 90 line -3 5 0 7 line -3 -8 0 -10 line 0 -10 0 7 cutcircle 4 -2 3 9 90 cutcircle 4 -2 8 9 80"
    option "Notifications" "w 2 cutcircle 0 -6 4 18 80 cutcircle 0 2 8 0 80 w 2.5 line -10 -5 10 -5 line -10 -5 -8 4 line 10 -5 8 5"
    option "Boot Applications" "w 2.5 cutcircle 0 0 10 18 130 line 0 4 0 10"
    option "Storage" "w 3 line -10 10 5 10 cont 10 5 cont 10 -10 cont -10 -10 cont -10 10 line -4 5 2 5 w 8 line 0 -2 0 -2"
    option "Multitasking" "w 2 line -10 5 0 10 line 10 5 0 10 line 0 0 -10 5 line 0 0 10 5 line -10 -1 0 -6 line 10 -1 0 -6 line -10 -7 0 -12 line 10 -7 0 -12"
    option "Community" "w 2 cutcircle 0 4 5 0 180 line -6 -5 6 -5 line 6 -5 8 -10 line -6 -5 -8 -10 line -8 -10 8 -10"
  )
  if subcategory == "Info" (
    loc 2 2 100 -100
    kernel = origin."kernel"
    square 0 0 140 : c#444
    square 0 0 130 : c#222
    icon "origin-logo" 3 : c#fff
    loc 2 2 200 -50
    text "originOS" 15
    text "" + kernel."name" 12
    loc 2 2 200 -75
    text "Version" + origin."version" 8
    loc 2 2 200 -110
    text "Kernel:" + kernel."name" + kernel."version" + kernel."type" 8
    loc 2 2 200 -130
    text "Storage: " ++ used_big ++ unit 8
    loc 2 2 200 -150
    text "UI Style: Origin-Default" 8
    loc 2 2 200 -170
    if system_url.contains("raw.githack.com/Mistium/Origin-OS") (
      text "BIOS: Official" 8
    ) else (
      if system_url.left(7) == "file://" (
        text "BIOS: Local" 8
      ) else (
        if system_url.contains("/editor?") (
          text "BIOS: Editor" 8
        ) else (
          text "BIOS: Modded" 8
        )
      )
    )
    loc 2 2 200 -210
    text "Device" 12
    loc 2 2 200 -240
    text "Memory:" + mem ++ "gb" 8
    loc 2 2 200 -260
    text "Cpu cores:" + cores 8
  )
  if subcategory == "Display" (
    loc 2 2 10 -20
    text `Origin Resolution: ${screensize_x}x${screensize_y}` 10 : c#fff
    loc 2 2 10 -100
    text "Render Mode" 10 : c#fff
    loc 2 2 60 -130
    square 100 20 10 : c#333
    if clicked (
      terminal "system display pixelated false"
    )
    text "Default" 10 : chx#-50 c#fff
    loc 2 2 180 -130
    square 100 20 10 : c#333
    if clicked (
      terminal "system display pixelated true"
    )
    text "Pixelated" 10 : chx#-50 c#fff
    terminal "system display get invert"
    toggle_invert = data.divide(100).round.bool
    loc 2 2 10 -180
    c #fff
    text "Invert Colours" 10
    toggle "invert" 20 : c#333 chx#30
    inp = toggle_invert.int * 100
    terminal "system display set invert" + inp.str
    loc 2 2 10 -220
    c #fff
    text "Night Mode" 10
    terminal "flags nightmode get"
    toggle_nightmode = data
    toggle "nightmode" 20 : c#333 chx#70
    terminal "flags nightmode set" + toggle_nightmode.str
  )
  if subcategory == "Media" (
    if media == "" (
      terminal "get access camera"
      data2 = data
      terminal "get access microphone"
      if "granted" in ( data + data2 ) (
        terminal "get mediadevices"
        media = data
        keys = media.getall("keys")
        types = media.getall("values").getall("type")
      ) else (
        media = "Please Grant Origin Microphone And Camera Permissions"
      )
    )
    loc 2 2 10 -40 + scroll_y
    text "System Volume" 10 : c#fff
    loc 999 2 0 -90 + scroll_y
    terminal "system sound get volume"
    slider_volume = data / 100
    slider frame_width - 50 20 "volume" 32 : c#111
    terminal "system sound set volume" + ( slider_volume * 100 )
    c #fff
    loc 2 2 10 -160 + scroll_y
    if media.istype("object") (
      text "Media Devices" 10
      count = 0
      loop keys.len (
        count ++
        y = count + 2.5 * -60 + scroll_y
        loc 999 2 0 y
        square frame_width - 20 40 10 : c#333 hover_c#252525
        cur = keys.[count]
        loc 2 2 30 y
        c #555
        square 30 30 10
        c #fff
        type = types.[count]
        if type == "audiooutput" or type == "audioboth" (
          icon "w 2 cutcircle -3 -1.5 6.5 -9 90 line -3 5 0 7 line -3 -8 0 -10 line 0 -10 0 7 cutcircle 4 -2 3 9 90 cutcircle 4 -2 8 9 80" 0.7
        )
        if type == "audioinput" (
          icon "w 3 line 0 -8 0 -5 line -4 -10 4 -10 cutcircle 0 2 7 18 70 w 7 line 0 2 0 8" 0.7
        )
        if type == "videoinput" (
          icon "w 3 line 8 -6 8 6 cont -4 6 cont -4 -6 cont 8 -6 cont -4 -6 cont -4 0 cont -10 -5 cont -10 5 cont -4 0" 0.7
        )
        text cur 9 : chx#30
      )
      page_len = keys.len + 2.5 * 60
    ) else (
      text media 10
      page_len = 100
    )
  ) else (
    media = ""
  )
  if subcategory == "Boot Applications" (
    boot_apps = user."onboot"
    if new_transmit (
      file "open" transmit_data
      f = file.[3] ++ "/" ++ file.[2] ++ file.[1]
      boot_apps.append(f)
      network "update" "onboot" boot_apps
      new_transmit = false
    )
    loc 2 2 20 -20 + scroll_y
    text "Reset Boot To Default: " 10 : c#fff
    icon "w 4 cutcircle 0 -2 11 13.5 150 line -6 9 0 15 line -6 10 0 3" 0.7 : chx#20
    if clicked (
      boot_apps = ["Origin/(A) System/System Apps/Desktop.osl","Origin/(A) System/Docks/Dock.osl","Origin/(A) System/System Apps/Quick_Settings.osl"]
      network "update" "onboot" boot_apps
    )
    loc 2 2 20 -70 + scroll_y
    text "Drop File Here" 9 : c#fff
    if file_dropped != 0 (
      file "open" file_dropped "onlyaccess"
      file "get" 3
      d3 = data
      file "get" 2
      d2 = data
      file "get" 1
      f = d3 ++ "/" ++ d2 ++ data
      boot_apps = boot_apps.append(f)
      network "update" "onboot" boot_apps
      file_dropped = 0
    )
    loc 2 2 20 -100 + scroll_y
    text "Click here to pick an osl file" 9
    if clicked and can (
      file "pick" "osl"
      can = false
    )
    count = 0
    loop boot_apps.len (
      count ++
      y = count + 2 * -60 + scroll_y
      loc 999 2 0 y
      square frame_width - 20 40 10 : c#333 hover_c#252525
      loc 2 2 15 y
      text boot_apps.[count] 9 : c#fff
      loc -2 2 -30 y
      square 30 30 10 : c#555 hover_c#666
      if onclick (
        boot_apps.delete(count)
        network "update" "onboot" boot_apps
      )
      icon "bin" 0.8 : c#fff
    )
    page_len = boot_apps.len + 2 * 60
  )
  if subcategory == "Storage" (
    file "open" "Origin/(C) Users.folder"
    y = -160 + 75 + scroll_y
    loc 999 2 0 y
    square frame_width - 30 140 10 1 : c#222
    loc 2 2 80 y
    icon file.[11] 3.3
    text "(Network)" + username 15 : chx#80 chy#20 c#fff
    change_x 20
    if network_drive.[2] == 10000000 (
      text "Free User" 10
      text "Upgrade: " 10 : c#ccc chx#20
      square 120 20 10 : c#ededed chx#100
      if clicked (
        network "new_tab" "https://www.patreon.com/user?u=51594743"
      )
      image "https://raw.githubusercontent.com/Mistium/Origin-OS/main/OSL%20Programs/assets/Settings/originOS%20patreon%20badge.png" null 20
    )
    if network_drive.[2] == 50000000 (
      tier = "Supporter"
    )
    if network_drive.[2] == 1000000000 (
      tier = "originDrive"
    )
    if network_drive.[2] == 5000000000 (
      tier = "origin Pro"
    )
    if network_drive.[2] > 999999 (
      used_big = network_drive.[2].divide(1000000).round
      unit = "mb"
    )
    if network_drive.[2] > 999999999 (
      used_big = network_drive.[2].divide(1000000000).round
      unit = "gb"
    )
    loc 2 2 160 y
    text network_drive.[1] / 1000 ++ "kb /" + used_big ++ unit 10
    loc 999 2 125 / 2 y - 35
    percent = network_drive.[1] / network_drive.[2]
    bar frame_width - 200 20 10 percent : c#444
    page_len = 460

    if total_files == 0 (
      file "open" current.[1] "onlyaccess"
      file "get" 4
      dat = data.str
      file = dat.len + "bytes"
      file "get" 2
      file = " " * ( 15 - file.len ) + file + "-" + data
      file "get" 1
      file.append(data)
      files.append(file)
      current.delete(1)
      file "get" 1
      if data == ".folder" (
        file "get" 4
        current = current ++ data
      )

      goto 0 scroll_y
      direction timer.multiply(100).sin * 360
      icon "w 2 cutcircle 0 0 10 0 90" 3 : c#fff

      if current.len == 0 (
        total_files = files.len
        file_data = files.sort("ascending").reverse.trim(1,10)
      )
    ) else (
      loc 2 2 20 -200 + scroll_y
      text "Biggest Files" 9 : c#fff
      for "i" file_data.len (
        loc 2 2 20 i * -30 - 200 + scroll_y
        text file_data.[i] 9
      )
      page_len = file_data.len * 30 + 200
    )
  )
  if subcategory == "Community" (
    loc 2 2 110 -30
    button "My Patreon" 200 25 10 : c#333
    if clicked and can (
      can = false
      http "newtab" "https://www.patreon.com/user?u=51594743"
    )
    loc 2 2 10 -70
    text "Patreon Supporters:" 10 : c#fff
    supporters = ["--- OriginDrive ---","Vernacular","Pnada","","--- Supporter ---","Pablo Estevez","Milodev123","","--- Free ---","Viofile"]
    
    loc 2 2 10 -100
    count = scroll_y.divide(20).round
    loop frame_height.divide(20).round (
      count ++
      loc 2 2 10 count * -20 + scroll_y - 100
      text supporters.[count] 10
    )
    page_len = supporters.len * 20 + 100
  )
  clicked = false
endef

def "network_page"
  loc 999 2 0 -70
  square frame_width - 30 100 10 : c#222
  if network."connected".not (
    loc 2 2 100 -90
    square 140 30 10 : c#333
    if clicked (
      terminal "connect 3"
    )
    text "Reconnect" 10 : c#fff chx#-60
    loc 2 2 30 -40
    temp = "Not Connected"
    square 0 0 10 : c#ae2525
  )
  if network."connected" (
    loc 2 2 30 -40
    temp = "Connected"
    square 0 0 10 : c#24ad2c
  )
  text temp 10 : c#fff chx#15
endef

def "Appearance"
  if subcategory == "none" (
    option "Background" "w 3 c #fff square 0 0 10 10 line 5 -5 3 -3 cont 2 -4 line 1 -5 -2 -2 cont -5 -5 w 6 dot 3 4"
    option "Dock" "w 6 line -7 -7 0 -7 line 7 -7 7 -7"
    option "Colours" "w 3 c #fff cutcircle 0 -3 5 18 120 cont 0 8 cont 4.9 -1.5 line 4 -3 -4 -3 line 4 -5.5 -4 -5.5"
    option "File Types" "w 4 line -8 10 3 10 cont 8 5 cont 8 -10 cont -8 -10 cont -8 10"
    option "Mouse" "line -9 9 7 -1 cont -5 -6 cont -9 9 line 5.8 -10 -9 9 line 0 0 -5 -5"
  )
  if subcategory == "Background" (
    loc 2 2 15 -30 + scroll_y
    text "Paste Image Url" 10 : c#fff
    change_x 20
    square 25 25 10 : c#333
    if clicked and can (
      can = false
      square 25 25 12 : c#444
      current_wallpaper = clipboard.str
    )
    icon "w 4 square 0 0 7 8 line -2 6 2 6 line 0 10 0 10" 0.7 : c#fff
    current_wallpaper.str
    if wallpapers.contains(current_wallpaper).not (
      if current_wallpaper.imageinfo("loaded") (
        wallpapers = wallpapers + current_wallpaper
        save "wallpapers.json" "set" wallpapers
        current_wallpaper = ""
      )
      if current_wallpaper == clipboard.str (
        image current_wallpaper 0 0
        text "Trying" 10 : c#fff chx#20
        image current_wallpaper 10
      )
    )
    wallpaper_data = "Wallpapers:" + wallpapers.len
    loc -2 2 -15 - ( wallpaper_data.len * 10 ) -30 + scroll_y
    text wallpaper_data 10
    count = 0
    loop wallpapers.len (
      count ++
      yc = count / 2
      y = yc.round * -230 + 50 + scroll_y
      loc -4 2 0 y
      if count % 2 == 1 "loc 4 2 0 y"
      w = frame_width / 2 - 30
      square w 200 10 : c#222
      cur = wallpapers.[count]
      image cur w 200
      change_x -40
      square 20 20 20 : c#222
      if mouse_touching (
        if clicked.not "square 20 20 23 : c#333"
        if clicked and can (
          terminal "system wallpaper" + cur
          network "update" "wallpaper" cur
          can = false
        )
      )
      icon "tick" 0.7 : c#fff
      change_x 80
      square 20 20 20 : c#222
      if mouse_touching (
        if clicked.not "square 20 20 23 : c#333"
        if clicked and can (
          wallpapers.delete(count)
          can = false
          save "wallpapers.json" "set" wallpapers
        )
      )
      icon "bin" 0.7 : c#fff
    )
    page_len = yc.round * 230 + 50
  )
  if subcategory == "Dock" (
    if new_transmit (
      file "open" transmit_data
      f = file.[3] ++ "/" ++ file.[2] ++ file.[1]
      segments.append(f)
      save "segments.txt" "set" segments
      global "segments"
      new_transmit = false
    )
    loc 2 2 20 -20 + scroll_y
    text "Reset Dock To Default: " 10 : c#fff
    icon "w 4 cutcircle 0 -2 11 13.5 150 line -6 9 0 15 line -6 10 0 3" 0.7 : chx#20
    if clicked (
      segments = ["Origin/(A) System/Docks/Modules/main.ode","Origin/(A) System/Docks/Modules/applications.ode","Origin/(A) System/Docks/Modules/folders.ode","Origin/(A) System/Docks/Modules/time.ode","Origin/(A) System/Docks/Modules/battery.ode"]
      save "segments.txt" "set" segments
      terminal "kill \"Origin/(A) System/Docks/Dock.osl\""
      wait 0.1
      window "add" "Origin/(A) System/Docks/Dock.osl"
      global "segments"
    )
    loc 2 2 20 -70 + scroll_y
    text "Drop File Here" 9 : c#fff
    if file_dropped != 0 (
      file "open" file_dropped "onlyaccess"
      file "get" 3
      d3 = data
      file "get" 2
      d2 = data
      file "get" 1
      f = d3 ++ "/" ++ d2 ++ data
      segments.append(f)
      save "segments.txt" "set" segments
      global "segments"
      file_dropped = 0
    )
    loc 2 2 20 -100 + scroll_y
    text "Click here to pick an ode file" 9
    if clicked and can (
      file "pick" "ode"
      can = false
    )
    count = 0
    loop segments.len (
      count ++
      y = count + 2 * -60 + scroll_y
      loc 999 2 0 y
      square frame_width - 20 40 10 : c#333 hover_c#252525
      loc 2 2 15 y
      text segments.[count] 9 : c#fff
      loc -2 2 -30 y
      square 30 30 10 : c#555 hover_c#666
      if clicked and can (
        can = false
        segments.delete(count)
        save "segments.txt" "set" segments
        global "segments"
      )
      icon "bin" 0.8 : c#fff
      if count > 1 (
        square 30 30 10 : c#555 hover_c#666 chx#-45
        if clicked and can (
          can = false
          count2 = count - 1
          segments.swap(count2,count)
          save "segments.txt" "set" segments
          global "segments"
        )
        icon "up" 0.8 : c#fff
      )
      if count < segments.len (
        square 30 30 10 : c#555 hover_c#666 chx#-45
        if clicked and can (
          can = false
          count2 = count + 1
          segments.swap(count,count2)
          save "segments.txt" "set" segments
          global "segments"
        )
        icon "down" 0.8 : c#fff
      )
    )
    page_len = segments.len + 2 * 60
  )
  if subcategory == "Colours" (
    if user."dock_colour".[1] != "#" (
      network "update" "dock_colour" #060606
    )
    if user."icon_colour".[1] != "#" (
      network "update" "icon_colour" #f3f3f3
    )
    if accent_select.[1] != "#" (
      colourpicker "setcol" global_accent
      accent_select = global_accent
      colourpicker "setcol" user."dock_colour"
      dock_select = user."dock_colour"
      colourpicker "setcol" user."icon_colour"
      icon_select = user."icon_colour"
    )
    loc 2 2 15 -30
    text `Accent Colour: ${accent_select}` 10 : c#fff
    change_x 30
    square 0 0 25 0 : c#fff
    square 0 0 20 0 : c#accent_select
    if clicked (
      colourpicker "setcol" accent_select
      colourpicker "show"
      selected_col = "accent"
    )
    if global_accent != accent_select (
      icon "w 40 dot c #fff w 4 cutcircle 0 -2 11 13.5 150 line -6 9 0 15 line -6 10 0 3" 0.6 : c#333 chx#30
      if clicked (
        colourpicker "setcol" global_accent
        accent_select = global_accent
      )
      loc -2 2 -120 -30
      square 200 20 15 0 1
      if onclick (
        network "update" "accent" accent_select
      )
      c #222
      if mouse_touching and clicked.not (
        square 200 25 12
      ) else (
        square 200 25 10
      )
      text "Sync With Network" 9 : c#fff chx#-95
      if mouse_down.not "can = true"
    )

    loc 2 2 15 -80
    text `Dock Colour: ${dock_select}` 10 : c#fff
    change_x 30
    square 0 0 25 0 : c#fff
    square 0 0 20 0 : c#dock_select
    if clicked (
      colourpicker "setcol" dock_select
      colourpicker "show"
      selected_col = "dock"
    )
    if user."dock_colour" != dock_select (
      icon "w 40 dot c #fff w 4 cutcircle 0 -2 11 13.5 150 line -6 9 0 15 line -6 10 0 3" 0.6 : c#333 chx#30
      if clicked (
        colourpicker "setcol" user."dock_colour"
        dock_select = user."dock_colour"
        selected_col = null
      )
      loc -2 2 -120 -80
      square 200 20 15 0 1
      if clicked and can (
        can = false
        network "update" "dock_colour" dock_select
      )
      if mouse_down.not "can = true"
      c #222
      if mouse_touching and clicked.not (
        square 200 25 12
      ) else (
        square 200 25 10
      )
      text "Sync With Network" 9 : c#fff chx#-95
      if mouse_down.not "can = true"
    )

    loc 2 2 15 -130
    text `Dock Secondary: ${icon_select}` 10 : c#fff
    change_x 30
    square 0 0 25 0 : c#fff
    square 0 0 20 0 : c#icon_select
    if clicked (
      colourpicker "setcol" icon_select
      colourpicker "show"
      selected_col = "icon"
    )
    if user."icon_colour" != icon_select (
      icon "w 40 dot c #fff w 4 cutcircle 0 -2 11 13.5 150 line -6 9 0 15 line -6 10 0 3" 0.6 : c#333 chx#30
      if clicked (
        colourpicker "setcol" user."icon_colour"
        icon_select = user."icon_colour"
        selected_col = null
      )
      loc -2 2 -120 -130
      square 200 20 15 0 1
      if clicked and can (
        can = false
        network "update" "icon_colour" icon_select
      )
      if mouse_down.not "can = true"
      c #222
      if mouse_touching and clicked.not (
        square 200 25 12
      ) else (
        square 200 25 10
      )
      text "Sync With Network" 9 : c#fff chx#-95
      if mouse_down.not "can = true"
    )
    if selected_col == "accent" (
      accent_select = picker_colour
    )
    if selected_col == "dock" (
      dock_select = picker_colour
    )
    if selected_col == "icon" (
      icon_select = picker_colour
    )
  )
  if subcategory == "File Types" (
    count = 0
    loop ftkeys.len (
      count ++
      y = count * -60 + scroll_y + 20
      loc 999 2 0 y
      square frame_width - 20 40 10 : c#333 hover_c#252525
      loc 2 2 30 y
      c #555
      square 30 30 10
      c #fff
      ftv = ftvalues.[count]
      icon ftv.[1] 0.8
      c #fff
      text `${ftv.[2]} (${ftkeys.[count]})` 10 : chx#30
    )
    page_len = count * 60 - 20
  )
  if subcategory == "mouse" (
    loc 2 2 10 -20
    text "Scroll Wheel Speed" 9 : c#fff
    terminal "get scrollspeed"
    toggle_invert = data.[1] != "-"
    slider_scrollsp = data.abs.clamp(-10,10)
    if slider_scrollsp.abs < 1 (
      if slider_scrollsp > 0 (
        slider_scrollsp = 1
      ) else (
        slider_scrollsp = -1
      )
    )
    loc 2 2 10 -90
    text "Scrolling " ++ ( ( slider_scrollsp * 10 ).round / 10 ) ++ "x speed" 9 : c#fff
    slider_scrollsp --
    slider_scrollsp /= 9
    c #222
    loc 999 2 0 -60
    slider frame_width - 30 20 "scrollsp" 10
    slider_scrollsp *= 9
    slider_scrollsp ++
    loc 2 2 10 -140
    text "Invert Scroll Direction" 9 : c#fff
    change_x 30
    toggle "invert" 20 : c#333
    if toggle_invert (
      temp = slider_scrollsp
    ) else (
      temp = slider_scrollsp * -1
    )
    terminal "system scrollspeed" + temp
    if temp != user."scrollspeed" (
      loc -2 2 -120 -140
      square 200 20 15 0 1
      if onclick (
        network "update" "scrollspeed" temp
      )
      c #222
      if mouse_touching and clicked.not (
        square 200 25 12
      ) else (
        square 200 25 10
      )
      text "Sync With Network" 9 : c#fff chx#-95
    )
  )
  frame "clear"
endef

def "account"
  loc 999 2 5 -90
  square frame_width - 25 150 10 : c#111
  loc 2 2 90 -90
  image user_icon.str 90 90
  square 90 90 12 0 1
  if clicked (
    square 25 25 12 0 1
    current_pfp = clipboard.str
    upload_time = timer
  )
  acc_pfp_button_clicked = clicked
  if mouse_touching (
    square 25 25 12 : c#222
    icon "w 4 square 0 0 7 8 line -2 6 2 6 line 0 10 0 10" 0.7 : c#fff
    loc 2 2 90 -50
    square 81 10 20 : c#222
    text "Paste Url" 9 : chx#-40.5 c#fff
  )
  loc 2 2 150 -80
  text username 12 : c#fff
  loc 2 2 150 -100
  text "origin Account" 10
  loc 2 2 150 -120
  text tier 10
  loc 2 2 20 -200
  text "Profile Picture:" 10
  loc 2 2 20 -230
  text "Click to paste an image URL as your profile picture:" 8.5 : c#fff
  change_x 20
  square 25 25 10 : c#333
  if clicked (
    square 25 25 12 : c#444
    current_pfp = clipboard.str
    upload_time = timer
  )
  icon "w 4 square 0 0 7 8 line -2 6 2 6 line 0 10 0 10" 0.7 : c#fff
    if timer - upload_time > 5 and current_pfp != "" (
      current_pfp = ""
      say "Failed to load pfp, try a different   website or try again"
    )
    if current_pfp.imageinfo("loaded") and ( acc_pfp_button_clicked or clicked ) (
      network "update" "pfp" current_pfp
      current_pfp = null.str
    )
    if current_pfp == clipboard.str (
      loc 2 2 20 -250
      image current_pfp 0 0
      text "Trying..." 10 : c#fff chx#20
    )
  )
  loc 2 2 200 -300
  c #333
endef

def "time"
  loc 999 2 0 -40
  square frame_width - 20 40 10 1 : c#222
  loc 999 2 0 -100
  square frame_width - 20 40 10 1
  loc 999 2 0 -160
  square frame_width - 20 40 10 1
  loc 2 2 20 -40
  text `Timezone: ${timezone}` 8 : c#fff
  loc 2 2 20 -100
  milisecond = timestamp.right(3)
  text `Time: ${day_number}:${month_number}:${year} ${hour}:${minute}:${second}:${milisecond}` 8
  loc 2 2 20 -160
  dt = delta_time * 1000
  dt = dt.round
  text `Since Last Frame: ${dt}ms` 8
endef

def "updates"
  version = origin.key("version")
  loc 2 2 20 -20 + scroll_y
  text "Current Version:" + version 10 : c#fff
endef

def "credits"
  if subcategory == "none" (
    page_len = 290
    w = frame_width / 2 - 40
    loc 4 2 2.5 -120 + scroll_y
    square w 200 20 : c#222
    square w 150 15 : chy#25 c#global_accent
    square w 150 8 : c#222
    c #fff
    x = x_position
    change_x -65
    icon "w 2 cutcircle 0 0 12 4.5 150 cutcircle 0 0 7 5 150 line -2 -4 -8 -10" 1.3
    text "roturTw" 15 : chx#30
    change -25 -18
    text "v5" 8
    set_x x - ( w / 2 - 10 )
    change_y -90
    text "Powering originNetworking" 9
    set_x x
    change_y -70
    square w 40 20 : c#222
    if clicked (
      subcategory = "Extensions"
      extensions = "JSON.stringify(Object.keys(Object.fromEntries(vm.extensionManager._loadedExtensions)))".eval
      extensions.prepend("Total:" + extensions.len)
    )
    change_x w / -2 + 10
    text "Full Extensions List >" 9 -100 : c#fff
    loc -4 2 -2.5 -95 + scroll_y
    square w 150 20 : c#222
    x = x_position
    change w / -2 + 30 40
    image "https://raw.githubusercontent.com/Mistium/Origin-OS/main/Resources/turbowarp_logo.png" 50 50
    change 30 12
    text "Turbowarp" 12 : c#fff
    change -162 -32
    text "      Thanks to GarboMuffin and the scratch team for making turbowarp possible".wrapText(w / 9 - 1) 9
    set_x x
    change_y -175
    square w 100 20 : c#222
    set_x 20
    change_y 40
    text "Github Contributors >" 9 : c#fff
    set_x 30
    change_y -30
    i = 0
    loop c_imgs.len (
      i ++
      image c_imgs[i].str 25 25
      change_x 30
    )
  )
  if subcategory == "Extensions" (
    count = 0
    loop extensions.len (
      count ++
      y = count - 0.5 * -60 + scroll_y
      loc 999 2 0 y
      square frame_width - 20 40 10 : c#222
      loc 2 2 15 y
      text extensions[count] 9 : c#fff
    )
    page_len = extensions.len - 0.5 * 60
  )
endef

if passed_data == "" (
  category = "System"
  subcategory = "none"
) else (
  passed_data.split("/")
  category = passed_data.[1]
  subcategory = passed_data.[2]
)

save "settings" "set_directory"
if save_data != "Success" (
  say "Failed To Access Save Directory"
  window "stop"
)
file "open" user_folder ++ ".folder"
current = file.[4]
files = []
file_data = ""
total_files = 0
contributors = [{"login":"Mistium","avatar_url":"https://avatars.githubusercontent.com/u/92952823?v=4"},{"login":"koo1140","avatar_url":"https://avatars.githubusercontent.com/u/112756430?v=4"},{"login":"RattusDong","avatar_url":"https://avatars.githubusercontent.com/u/147535283?v=4"},{"login":"vernacular7","avatar_url":"https://avatars.githubusercontent.com/u/132183833?v=4"},{"login":"MILO123PRO","avatar_url":"https://avatars.githubusercontent.com/u/64213421?v=4"},{"login":"Rainbow10000","avatar_url":"https://avatars.githubusercontent.com/u/144138634?v=4"},{"login":"ThePandaDever","avatar_url":"https://avatars.githubusercontent.com/u/73702185?v=4"},{"login":"Dighoster9797","avatar_url":"https://avatars.githubusercontent.com/u/141959187?v=4"},{"login":"Kasairoe","avatar_url":"https://avatars.githubusercontent.com/u/146598325?v=4"}]
c_names = contributors.destr.getall("login")
c_imgs = contributors.destr.getall("avatar_url")

window "dimensions" 880 500
dock_location = dock_location
media = ""
segments = segments
accent_select = ""
tier = null
network_drive = "none"
gotosubcategory = ""
current_pfp = ""
save "wallpapers.json" "get"
wallpapers = save-data
if wallpapers.len < 1 or wallpapers.istype("array").not (
  wallpapers = [
    "\"https://raw.githubusercontent.com/Mistium/Origin-OS/main/Wallpapers/originOS%20Rift.png\"",
    "\"https://raw.githubusercontent.com/Mistium/Origin-OS/main/Wallpapers/originOS%20Flow.png\"",
    "\"https://images.unsplash.com/photo-1696193846016-f0a30c035e70\"",
    "\"https://images.pexels.com/photos/589810/pexels-photo-589810.jpeg\"",
    "\"https://images.pexels.com/photos/1612351/pexels-photo-1612351.jpeg\"",
    "\"https://images.pexels.com/photos/640781/pexels-photo-640781.jpeg\"",
    "\"https://images.pexels.com/photos/7919/pexels-photo.jpg\"",
    "\"https://images.unsplash.com/photo-1697048851717-81d7cbee7ed2\"",
    "\"https://images.pexels.com/photos/1366957/pexels-photo-1366957.jpeg\""
  ]
  save "wallpapers.json" "set" wallpapers
)
save "segments.txt" "set" segments

// file type pages
ftkeys = file_types.getKeys()
ftvalues = file_types.getValues()

mainloop:
loc 999 2 0 -25
square window_width - 7.5 15 30 1 : c#333
loc 2 999 100 0
square 220 window_height 10 : c#111
loc 2 2 25 -25
DrawCategory "System" "w 2 cutcircle 0 0 12 4.5 150 cutcircle 0 0 7 5 150 line -2 -4 -8 -10"
DrawCategory "Network" "w 2 cutcircle 0 -5 14 0 50 cutcircle 0 -5 7 0 40 w 4 dot 0 -6"
DrawCategory "Appearance" "w 3 cutcircle 5 6 3 45 80 line 7.5 4 -4.5 -9 line 3.5 8.5 -9 -5 line -9 -5 -11 -11 line -4.5 -9 -11 -11"
DrawCategory "Account"
DrawCategory "Time" "w 2 cutcircle 0 0 12 0 180 w 3 line 0 0 0 5 line 0 0 3 -3"
DrawCategory "Privacy" "w 12 dot 2.5 2.5 w 5 line 2.5 2.5 -7 -7 w 3 line -6.5 -6.5 -3.5 -9.5 line -4 -4 -1 -7"
DrawCategory "Updates" "w 3 cutcircle 0 -2 9 13.5 150 line -6 8 0 14 line -6 8 0 0"
DrawCategory "Credits" "w 3 cutcircle -4.5 -10 5.5 0 90 cutcircle 4 -5.5 6 3 60 cutcircle -5 5 2 0 180 cutcircle 5 8 2 0 180"
loc 2 2 25 -175
image user_icon.str 35 35
loc 2 2 230 -25
if subcategory == "none" (
  text category 10 : c#fff
) else (
  square 20 20 10 : c#555 hover_c#666 chx#10
  icon "left-arrow" 0.6 : c#fff
  if clicked (
    subcategory = "none"
  )
  change_x 20
  text subcategory 10
)
loc 2 2 215 -48
x = x_position
y = y_position
loc -2 -2 0
c #333
frame x y x_position y_position page_len
page_len = 0
y = -30 + scroll_y
startx = x_position
location = category + subcategory
if category == "System" "system"
if category == "Network" "network_page"
if category == "Appearance" "Appearance"
if category == "Account" "account"
if category == "Apps" "apps"
if category == "Time" "time"
if category == "Updates" "updates"
if category == "Credits" "credits"

if mouse_down.not "can = True"
frame "clear"

if gotosubcategory != "" and mouse_down.not (
  subcategory = gotosubcategory
  gotosubcategory = ""
)
import "win-buttons"
