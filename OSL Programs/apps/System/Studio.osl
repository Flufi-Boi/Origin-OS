window "show"
network "set_app_id" "Studio"

save "Studio" "set_directory"
save "recent_files.json" "get"
if save_data.istype("array") (
  recent = save_data
) else (
  save "recent_files.json" "set" []
  recent = []
)
recent_2 = recent.trim(1,10)

if passed_data == null (
  current_studio_page = "Home"
) else (
  current_studio_page = passed_data
)
sidebar_items = 0

window "set_dragbox" [[2,2,40,0],[-2,2,-150,-40]]

def "draw_buttons"
  loc -2 2 -20 -20

  square 20 20 10 : c#c_seco hover_size#1.1
  icon "close" 0.6 : c#c_text
  if onclick (
    window "stop"
  )

  square 20 20 10 : c#c_seco chx#-40 hover_size#1.1
  if mouse_touching "square 20 20 12"
  icon "down" 0.6 : c#c_text
  if onclick (
    window "minimise"
  )

  square 20 20 10 : c#c_seco chx#-40 hover_size#1.1
  if mouse_touching "square 20 20 12"
  icon "maximise" 0.6 : c#c_text
  if onclick (
    window "maximise"
  )
endef

sharing_with = null

mainloop:
theme = user.theme
c_prim = theme.primary
c_seco = theme.secondary
c_tert = theme.tertiary
c_text = theme.text

if new_transmit (
  if window.children.contains(transmit_source) (
    current_studio_page = transmit_data
  )
  new_transmit = false
)

if mouse_down.not "can = true"

if current_studio_page == "Recent Files" (
  loc 2 2 60 -22
  text "Recent Files" 10 : c#c_text

  loc 2 2 25 -25
  square 30 30 15 1 : c#c_prim
  square 30 30 10 1 : hover_c#c_seco
  if onclick (
    current_studio_page = "Home"
  )
  icon "left-arrow" 0.7 : c#c_text hover_size#1.2

  c c_prim
  frame window.left window.top - 50 window.right window.bottom recent.len - 0.5 * 60
  w = frame_width - 20
  len = recent.len
  count = ((scroll_y / 60).round - 1).clamp(0,len)
  loop (frame_height / 60 + 2).round.clamp(0,len) (
    count ++
    y = count * -60 + 30 + scroll_y
    loc 999 2 0 y
    square w 40 10 1 : c#c_prim cursor#pointer
    cur = recent[count]
    if mouse_touching (
      square w 40 15 1 : c#global_accent
      square w 40 10 1 : c#c_seco
      if onclick (
        current_studio_page = cur.tostr()
      )
    )
    change_x window_width - 90 / -2
    file "open" cur "onlyaccess"
    if loaded_file != 0 (
      file "render" 1.5
      loc 2 2 80 y
      file "get" 2
      name = data
      file "get" 1
      text name ++ data 9 : c#c_text
      loc -2 2 -160 y
      file "get" 10
      text data.timestamp("convert-relative") 8
    ) else (
      recent.delete(count)
      save "recent_files.json" "set" recent
      count --
    )
    file "close"
  )
  frame "clear"
  draw_buttons
  exit
)

if current_studio_page == "Home" (
  loc 2 2 30 -30
  image user_icon.tostr() 30
  text `Heya ${username}!` 10 : c#c_text chx#30

  w = window_width / 2
  w2 = w - 40
  loc 2 2 20 -85
  text "New File" 8

  loc 999 2 0 -80
  text "Recent Files" 8
  
  loc 2 2 w / 2 - 5 -290
  square w2 30 10 : c#c_seco
  if onclick (
    file "pick"
  )
  change_x w2 / -2 + 10
  text "Open File" 9 : c#c_text

  loc 2 2 w / 2 - 5 -125
  square w2 30 10 : c#c_prim
  input w2 - 10 25 "name" "hello world.txt" 10 c_text
  change_y -55
  if input_name.matchregex("/[^.]+\.[^.]+/gm") (
    square w2 30 10 : c#c_prim cursor#pointer
    if mouse_touching (
      square w2 30 15 : c#global_accent
      square w2 30 10 : c#c_seco
    )
    if selected_input == "name" and "enter".onpress or onclick (
      current_studio_page = input_name.tostr()
    )
    change_x w2 / -2 + 15 : c#c_text
  ) else (
    square w2 30 10 1 : c#c_seco
    change_x w2 / -2 + 15 : c#c_prim
  )
  c c_text
  icon "Add" 0.5
  text "Create New File" 8 : chx#20

  c c_prim
  frame -10 window_height / 2 - 95 window_width / 2 window_height / -2 recent_2.len * 55
  w = frame_width - 20
  for count recent_2.len (
    y = count * -55 + 25 + scroll_y
    loc 999 2 0 y
    square w 30 10 1 : c#c_prim cursor#pointer
    cur = recent_2[count]
    if mouse_touching (
      square frame_width - 20 30 15 1 : c#c_seco
      if onclick (
        current_studio_page = cur.str
      )
    )
    change w / -2 + 15
    file "open" cur.str "onlyaccess"
    if loaded_file !== 0 (
      file "render" 1
      loc 2 2 60 y
      file "get" 2
      name = data
      file "get" 1
      text name ++ data 9 : c#c_text
    ) else (
      recent.delete(count)
      save "recent_files.json" "set" recent
      count --
      recent2 = recent.trim(1,10)
    )
  )
  frame "clear"
  loc -2 2 -30 -80
  square 30 15 10 : c#c_prim hover_size#1.1
  if mouse_touching (
    cursor "pointer"
    if onclick (
      current_studio_page = "Recent Files"
    )
  )
  change -12 -1
  text "All" 8 : c#c_text
  draw_buttons
  exit
)

sharing_with.destr

if new_network_data (
  if packet.username == sharing_with (
    systemvars "setlist" "App // Data" packet.data
  )
  new_network_data = false
)

loc 2 999 75 -25
square 40 window_height - 50 10 1 : c#c_seco
loc 999 2 0 -22
square window_width 30 20 : c#c_prim
loc 2 999 25 0
square 35 window_height 20
if preview_open "right = window.right - 400" else "right = window.right"
draw_buttons

loc 2 2 25 -25
loop sidebar_items (
  square 30 30 15 : c#c_prim
  if mouse_touching (
    square 30 30 15 : c#c_seco
    square 30 30 mouse_left ? 7 10 : c#c_prim
  )
  change_y -50
)
sidebar_items = 0
